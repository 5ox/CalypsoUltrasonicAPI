Build1=Light,com.calypsoinstruments.CalypsoUltrasonicAPI,Light,NoAds
Build2=New_1,com.calypsoinstruments.CalysoUltrasonicAPI
File1=actBleActivity.bal
File2=actcalcomp.bal
File3=api_icon.png
File4=bt.png
File5=calypso_drawer.jpg
File6=connection.png
File7=MainActivity.bal
File8=textura_fibra_carbono.jpg
File9=ultra.jpg
FileGroup1=Default Group
FileGroup2=Default Group
FileGroup3=Default Group
FileGroup4=Default Group
FileGroup5=Default Group
FileGroup6=Default Group
FileGroup7=Default Group
FileGroup8=Default Group
FileGroup9=Default Group
Group=Default Group
IconFile=
Library1=core
Library2=gps
Library3=phone
Library4=ble2
Library5=byteconverter
Library6=javaobject
Library7=runtimepermissions
Library8=preferenceactivity
ManifestCode='This code will be applied to the manifest file during compilation.~\n~'You do not need to modify it in most cases.~\n~'See this link for for more information: https://www.b4x.com/forum/showthread.php?p=78136~\n~AddManifestText(~\n~<uses-sdk android:minSdkVersion="22" android:targetSdkVersion="27"/>~\n~<supports-screens android:largeScreens="true" ~\n~    android:normalScreens="true" ~\n~    android:smallScreens="true" ~\n~    android:anyDensity="true"/>)~\n~~\n~SetApplicationAttribute(android:icon, "@drawable/icon")~\n~SetApplicationAttribute(android:label, "$LABEL$")~\n~CreateResourceFromFile(Macro, Themes.DarkTheme)~\n~~\n~AddApplicationText(<activity android:name="anywheresoftware.b4a.objects.preferenceactivity"/>)~\n~~\n~' Permissions~\n~AddPermission(android.permission.ACCESS_NETWORK_STATE)~\n~AddPermission(android.permission.ACCESS_COARSE_LOCATION) 'no necesaria si no BluetoothAdmin~\n~AddPermission(android.permission.ACCESS_FINE_LOCATION)~\n~AddPermission(android.permission.BLUETOOTH)~\n~AddPermission(android.permission.BLUETOOTH_ADMIN)~\n~AddPermission(android.permission.WRITE_EXTERNAL_STORAGE)~\n~AddPermission(android.permission.RECEIVE_ULTRASONIC_API)~\n~~\n~'End of default text.~\n~
Module1=Starter
Module2=BackgroundService
Module3=actBLE
Module4=ComputationTools
Module5=actCalibration
NumberOfFiles=9
NumberOfLibraries=8
NumberOfModules=5
Version=8.3
@EndOfDesignText@
'--------------------------------------------------------------------------------------------------
' Main Activity (initial Activity launched upon program start)
' 
' API for the Calypso Ultrasonic Anemometer
' Calibrate the on-board compass
' Interface to 3rd party Apps using Broadcast Intents / Broadcast Receivers
'
' Developed by Volker Petersen (volker.petersen01@gmail.com)
' July 2018
' -------------------------------------------------------------------------------------------------
'
' TODO write calibration data to file https://www.b4x.com/android/forum/threads/text-files.6690/
' TODO implement calibration with Excel test to confirm
' TODO calibration as 2 step process. Step 1: calibrate compass, Step 2: calibrate device mount by
'      taking the difference between boat compass and device compass. That become the 'offsetAngle'.
' TODO test/complete the sample rate updates on the device
' TODO implement RichString library to get Superscript letters 
'
' to set App icon, select a file from /Files folder in Project->Choose Icon - file can have any name. 
' In Manifest add line 'SetApplicationAttribute(android:icon, "@drawable/icon")'.  This IDE will copy
' the file you chose in Project->Choose Icon to "/Objects/res/drawable/icon.png" so that the compiler
' will find it.

#Region  Project Attributes 
	#ApplicationLabel: Calypso Ultrasonic API
	#VersionCode: 1
	#VersionName: ver 1.0
	'SupportedOrientations possible values: unspecified, landscape or portrait.
	#SupportedOrientations: portrait
	#CanInstallToExternalStorage: False
	#BridgeLogger: True
#End Region
'#Extends: android.support.v7.app.AppCompatActivity

#Region  Activity Attributes 
	#FullScreen: False
	#IncludeTitle: True
#End Region

Sub Process_Globals
	'These global variables will be declared once when the application starts.
	'These variables can be accessed from all modules.
	Public uiTimer As Timer
	Public uiUpdateFrequency As Int = 1000
	Public yes As String = "Yes"
	Public no As String = "No"
End Sub

Sub Globals
	'These global variables will be redeclared each time the activity is created.
	'These variables can only be accessed from this module.
	Private btnCONNECT As Button
	Private lblAWA As Label
	Private lblCOG As Label
	Private lblAWS As Label
	Private lblBATTERY As Label
	Private lblConnectionStatus As Label
	Private lblTEMP As Label
	Private lblROLL As Label
	Private lblPITCH As Label
	Private lblCOMP As Label
	Private lblLOCATION As Label
	Private lblBROADCASTS As Label
	Private accValues(3) As Float
	Private magValues(3) As Float
	Private accGrav(3) As Float
End Sub

Sub Activity_Create(FirstTime As Boolean)
	'Do not forget to load the layout file created with the visual designer. For example:
	Activity.LoadLayout("MainActivity")

	'---------------------------------------------------------------------------------
	'ActionBar Menu       Title         Event
	Activity.AddMenuItem("Preferences","Preferences") 'top
	Activity.AddMenuItem("Bluetooth Scan","Bluetooth") 'middle
	Activity.AddMenuItem("Compass Calibration","Calibration") 'middle
	Activity.AddMenuItem("Exit App","Exit") 'bottom
		
	If FirstTime Then
		Starter.phoneAccelerometer.Initialize(Starter.phoneAccelerometer.TYPE_MAGNETIC_FIELD)
		Starter.phoneMagnetic.Initialize(Starter.phoneMagnetic.TYPE_MAGNETIC_FIELD)
		Create_User_Preference_Screen
		Starter.javaInline.InitializeContext
	End If
			
	btnCONNECT.Tag = "Scan"
	btnCONNECT.Text = "Scan for BLE"
	lblBROADCASTS.Text = " "
End Sub

Sub Activity_Resume	
	Get_Current_Preference_Settings(False)			' fetch the lastest User Preference settings

	'Starter.calcTools.test							' Kalman and Lowpass Filter testing completed

	' make sure Location Services are running
	If Starter.activeGPS = False Then
		Starter.localDeclination = 999.0			' force an update of the declination data
		Location_Services_Start
	Else
		Starter.rp.CheckAndRequest(Starter.rp.PERMISSION_ACCESS_FINE_LOCATION)
		Wait For Activity_PermissionResult(Permission As String, Result As Boolean)
		If Result Then CallSubDelayed(Starter, "StartGPS")
	End If
	
	' make sure BLE is not running before we start the the BLE service again 
	If Starter.bleEnabled = False Then
		Bluetooth_Services_Start
	End If
	lblConnectionStatus.Text = Starter.bleStateText
	
	'Starter.phoneAccelerometer.StartListening("Accelerometer")
	'Starter.phoneMagnetic.StartListening("Magnetic")
	accGrav(0) = 0.0
	accGrav(1) = 0.0
	accGrav(2) = 0.0
		
	' now start our Background Service (this function ensures that we always get restart the Service at any time)
	Launch_Background_Services
	
	' start the UI update timer with execution (clicks) every 'uiUpdateFrequency' milliseconds
	uiTimer.Initialize("uiUpdate", uiUpdateFrequency)
	uiTimer.Enabled=True
End Sub


Sub Activity_Pause (UserClosed As Boolean)
	Starter.phoneAccelerometer.StopListening
	Starter.phoneMagnetic.StopListening
	Starter.bleScanTimeout.Enabled = False
	
	' stop the uiTimer for the uiUpdate
	uiTimer.Enabled = False
	
	Log("User Closed: "&UserClosed)
End Sub


Sub Activity_KeyPress (KeyCode As Int) As Boolean
	' function to catch the back key and only exit the app when the user confirms this
	If KeyCode = KeyCodes.KEYCODE_BACK Then
		Dim question As String = "Do you really want to exit this App and shutdown the datafeed from the Anemometer?"
		Dim title As String = "Exit App?"
	
		If Confirm_Dialog(question, title, yes, no) Then
			Starter.bleManager.Disconnect
			StopService(BackgroundService)
			Starter.runBackgroundTasks = False	' the background services stops when this Boolean is False
			Starter.ctr_ble = 0					' reset the Broadcast Receiver send counter
			Return False						' tell the OS that I take care of canceling the app
			Activity.Finish 					'...or whatever other previous killing actions.
		Else
			Return True
		End If
	End If
	Return False
End Sub


Sub Launch_Background_Services
	If Starter.bleConnected Then
		Starter.runBackgroundTasks = True	' the background services runs until this Boolean is False
		StartService(BackgroundService)		' start Background Service for this App
		btnCONNECT.Text = "BLE Stop"
		Log("Main->Launch_Background_Services(): Background Services with Bluetooth status: " & Starter.bleManager.State)
	End If	
	Log("Main->Launch_Background_Services(): pref Mac: "&Starter.prefBluetoothMAC)

	' start the UI update timer with execution (clicks) every 'uiUpdateFrequency' milliseconds
	uiTimer.Initialize("uiUpdate", uiUpdateFrequency)
	uiTimer.Enabled=True
	update_UI
End Sub


Sub update_UI
	Dim str, tmp As String
	'Dim rs As RichString
	'Dim rsb As RichStringBuilder

	'rsb.Initialize
	'rsb.Addend(str)
	'rsb.Append ("({superscript}T{superscript}) ")
	
	lblAWA.Text = NumberFormat(Starter.sensorDataProcessed.Get("AWA"), 3, 0) & "º"
	lblAWS.Text = NumberFormat2(Starter.sensorDataProcessed.Get("AWS")*Starter.speedToKnots, 1, 1, 1, False)
	lblCOMP.Text = NumberFormat(Starter.sensorDataProcessed.Get("Compass"), 3, 0) & "º"
	lblROLL.Text = NumberFormat(Starter.sensorDataProcessed.Get("Pitch"), 3, 0) & "º"
	lblPITCH.Text = NumberFormat(Starter.sensorDataProcessed.Get("Roll"), 3, 0) & "º"
	lblBATTERY.Text = NumberFormat(Starter.sensorDataProcessed.Get("Battery"), 1, 0) & "%"
	lblTEMP.Text = NumberFormat2(Starter.sensorDataProcessed.Get("Temp"), 1, 1, 1, False) & "ºC"
	lblCOG.Text = NumberFormat(Starter.sensorDataProcessed.Get("COG"), 3, 0) & "º"
	lblConnectionStatus.Text = Starter.bleStateText
	
	If Starter.ctr_ble > 1 Then
		str = Starter.ctr_ble & " API data transmissions (speeds in m/s, angles in "
		If Starter.prefTrueNorth Then
			str = str & "True North)"
		Else
			str = str & "Magnetic North)"
		End If
		lblBROADCASTS.Text = str
	End If
	If Starter.activeGPS Then
		tmp = Starter.gpsLocation.ConvertToMinutes(Starter.sensorData.Get("LAT"))
		str = "Lat: " & tmp.SubString2(0, tmp.Length-2)
		tmp = Starter.gpsLocation.ConvertToMinutes(Starter.sensorData.Get("LON"))
		str = str & "   Lon: " & tmp.SubString2(0, tmp.Length-2)
	End If
	If Starter.activeCompass Then
		str = str & "   Hdg: " & NumberFormat(Starter.phoneCompass, 3, 0) & "º"
	End If
	If ( Abs(Starter.sensorData.Get("DEC")) > Starter.calcTools.precission ) Then
		str = str & "   δ: " & NumberFormat(Starter.sensorData.Get("DEC"), 1, 1) & "º"
	End If
	lblLOCATION.Text = str
End Sub


' uiTimer Event that get's executed every 'uiUpdateFrequency' miliseconds - see uiTimer.Initialize()
Sub uiUpdate_Tick
	update_UI
End Sub


' Layout Button click even
Sub btnCONNECT_Click
	Log("Main->btnCONNECT_Click(): " & btnCONNECT.Tag)

	If btnCONNECT.Tag = "Scan" Then
		btnCONNECT.Text = "Scanning...."
		CallSubDelayed(Starter, "BLE_Scan")
	Else
		Dim question As String = "Do you really want to shutdown the datafeed from the Anemometer?"
		Dim title As String = "Disconnect Bluetooth?"
	
		If Confirm_Dialog(question, title, yes, no) Then
			Starter.bleManager.Disconnect
			Starter.bleConnected = False
			Disconnect_Bluetooth
			btnCONNECT.Text = "Scan for BLE"
			' Stop our Background Service
			Starter.runBackgroundTasks = False	' stop the background services
		End If
	End If
	btnCONNECT.Invalidate
End Sub


Sub Connecting_Bluetooth
	btnCONNECT.Enabled = True
	Starter.bleStateText = "Connecting to " & Starter.actual_ultra.MacAddress
	lblConnectionStatus.Text = Starter.bleStateText
	btnCONNECT.Invalidate
	btnCONNECT.Text = "Connecting..."
	btnCONNECT.Tag = "Stop"
End Sub


Sub Disconnect_Bluetooth
	btnCONNECT.Enabled = True
	Starter.bleStateText = "Disconnected"
	lblConnectionStatus.Text = Starter.bleStateText
	btnCONNECT.Invalidate
	btnCONNECT.Text = "Scan & Connect BLE"
	btnCONNECT.Tag = "Scan"
End Sub


Sub Bluetooth_Services_Start
	'---------------------------------------------------------------------------------
	' Check the status of the Bluetooth device and power on as necessary
	
	If Starter.bleManager.State = Starter.bleManager.STATE_POWERED_ON Then 		 'BLE is powered on
		Log("Main->Bluetooth_Services_Start(): Bluetooth is powered on. Starting Scan....")
		Starter.bleEnabled = True
		
	Else If Starter.bleManager.State = Starter.bleManager.STATE_POWERED_OFF Then 'BLE is powered off

		Dim question As String = "Bluetooth is disabled. Do you want to enable it?"
		Dim title As String = "Bluetooth devise status"
		If Confirm_Dialog(question, title, yes, no) Then
			#if B4A
				Starter.bleEnabled = Starter.javaInline.RunMethod("setBluetooth", Null)
			Log("Main->Bluetooth_Services_Start(): Power on suceeded: " & Starter.bleEnabled)
			#end if
		Else
			Msgbox("Some App functions will not be available.", "Bluetooth warning")
			Starter.bleEnabled = False
			Log("Main->Bluetooth_Services_Start(): User declined to Bluetooth device on.")
		End If
		
	Else														 ' BLE is not supported
		Msgbox("Sorry, but it looks as if Bluetooth is not available on this device. Some App functions will not be available.", "Bluetooth Unavailable Error")
		Starter.bleEnabled = False
		Log("Main->Bluetooth_Services_Start(): No Bluetooth device found on this device.")
	End If

	If Starter.bleEnabled Then
		Starter.bleStateText = "BLE power on."
		lblConnectionStatus.Text = Starter.bleStateText
	Else
		Starter.bleStateText = "BLE is powered off!"
		lblConnectionStatus.Text = Starter.bleStateText
	End If
	
End Sub


Sub Location_Services_Start
	Dim question As String = "Location service is disabled. Do you want to enable it?"
	Dim title As String = "Location Services"
	If Confirm_Dialog(question, title, yes, no) Then
		StartActivity(Starter.gpsManager.LocationSettingsIntent)  ' will open the appropiate screen on device
		Starter.activeGPS = Starter.gpsManager.GPSEnabled
		Log("Main->Location_Services_Start(): activeGPS " & Starter.activeGPS)
	Else
		Msgbox("Please enable Location Services so that this App can compute TWA, TWD, & TWS", "Location Services")
	End If
End Sub


Sub Accelerometer_SensorChanged(Values() As Float)
	' in order to measure the real acceleration of the device, the contribution of 
	' the force of gravity must be eliminated. We do that here using a lowpass filter
	Dim alpha As Float = 0.8
	Dim i As Int
	
	For i=0 To 2
		accGrav(i) = accGrav(i)*alpha + Values(i)*(1-alpha)	' smooth with lowpass filter
		accValues(i) = Values(i) - accGrav(i)
	Next
End Sub

Sub Magnetic_SensorChanged(Values() As Float)
	magValues = Values
	Calc_Orientation
End Sub


'---------------------------------------------------------------------------------
' Compute the magnetic orientation of the Device
' https://www.b4x.com/android/forum/threads/orientation-and-accelerometer.6647/page-6#post-476271
' https://stackoverflow.com/questions/8315913/how-to-get-direction-in-android-such-as-north-west
' https://stackoverflow.com/questions/15315129/convert-magnetic-field-x-y-z-values-from-device-into-global-reference-frame
Sub Calc_Orientation()
	If accValues.Length = 0 Or magValues.Length = 0 Then		' TODO - this algo is not yielding correct Hdg
		'Starter.sensorData.Put("MAG", 0.0)
		Return
	Else
		#if B4A
			Dim R(9), I(9) As Float
			Dim success As Boolean = Starter.javaNative.RunMethod("getRotationMatrix", Array(R, I, accValues, magValues))
			If success Then
				
				Dim orientation(3), A_D(3), A_W(3) As Float
				Dim rad As Float
				rad = 180.0/cPI
				
				A_D(0) = magValues(0)
				A_D(1) = magValues(1)
				A_D(2) = magValues(2)
				
				A_W(0) = R(0) * A_D(0) + R(1) * A_D(1) + R(2) * A_D(2)
				A_W(1) = R(3) * A_D(0) + R(4) * A_D(1) + R(5) * A_D(2)
				A_W(2) = R(6) * A_D(0) + R(7) * A_D(1) + R(8) * A_D(2)
				
				A_W(0) = (A_W(0) * rad + 360.0) Mod 360.0
				A_W(1) = (A_W(1) * rad + 360.0) Mod 360.0
				A_W(2) = (A_W(2) * rad + 360.0) Mod 360.0

				
				Starter.javaNative.RunMethod("getOrientation", Array(R, orientation))
				Starter.phoneCompass = (orientation(0) * rad + 360.0) Mod 360.0
				'Starter.sensorData.Put("MAG", Starter.phoneCompass)
				Log("Main->Location_Services_Start(): Phone compass direction: " & NumberFormat(Starter.phoneCompass, 3, 0))
				Log("orientation(0): " & ( (orientation(0) * rad + 360.0) Mod 360.0) & "A_W(0) = " & A_W(0) )
				Log("orientation(1): " & ( (orientation(1) * rad + 360.0) Mod 360.0) & "A_W(1) = " & A_W(1) )
				Log("orientation(2): " & ( (orientation(2) * rad + 360.0) Mod 360.0) & "A_W(2) = " & A_W(2) )

				Starter.activeCompass = True
			End If
		#end if
	End If
End Sub


'---------------------------------------------------------------------------------
' ActionBar Menu options click actions

Sub Preferences_Click
	'Log("Main->Preferences_Click(): Calling the Preference Intent now...")
	StartActivity(Starter.prefScreen.CreateIntent)
End Sub


Sub Bluetooth_Click
	' force a full Bluetooth connection process (Power on, Scan, Connect)
	Log("Main->Bluetooth_Click(): " & btnCONNECT.Tag)
	Bluetooth_Services_Start
End Sub


Sub Calibration_Click
	' make sure we have the propper User Permission to write calibation data to file
	Starter.rp.CheckAndRequest(Starter.rp.PERMISSION_WRITE_EXTERNAL_STORAGE)
	Wait For Activity_PermissionResult(Permission As String, Result As Boolean)

	If (Result=False) Or (File.ExternalWritable=False) Then
		Msgbox2("Cannot write to the default storage location. Missing User permission. Can not start the calibration process", "Storage Access Error", "OK", "", "", Starter.appIcon)
		Return
	End If

	StartActivity(actCalibration)

End Sub


Sub Exit_Click
	Dim question As String = "Do you really want to exit this App and shutdown the datafeed from the Anemometer?"
	Dim title As String = "Exit App?"
	
	If Confirm_Dialog(question, title, yes, no) Then
		' Stop our Background Service
		Starter.bleManager.Disconnect
		Starter.runBackgroundTasks = False	' the background services stops when this Boolean is False
		Starter.ctr_ble = 0					' reset the Broadcast Receiver send counter
		Activity.Finish
	End If
End Sub


Sub Confirm_Dialog(question As String, title As String, positive As String, negative As String) As Boolean
	Dim feedback As Int
	'Log("Main->Confirm_APP_Exit(): " & question)
	feedback = Msgbox2( question, title, positive, "", negative, Starter.appIcon )
	'Log("feedback="&feedback&"  ="&DialogResponse.POSITIVE&"   "&(feedback = DialogResponse.POSITIVE))
	Return (feedback = DialogResponse.POSITIVE)
End Sub


'---------------------------------------------------------------------------------
' User preference settings: screen / defaults / updates

Sub Create_User_Preference_Screen
	Log("Main->Create_User_Preference_Screen(): Creating the User Preferences Screen for the first time...")
	Starter.prefScreen.Initialize("App Settings", "")
	
	'create two categories
	Dim cat1, cat2 As PreferenceCategory
	cat1.Initialize("Software Parameters")
	cat1.AddCheckBox("TrueNorth", "Direction Data", "Display direction data as True North (on) or Magnetic North (off)", False)
	cat1.AddList("SampleRate", "Sample Rate", "The higher the frequency, the higher the power consumption.", _
        "4 samples/second", Array As String("1 sample/second", "4 samples/second", "8 samples/second"))
	'cat1.AddCheckBox("Smoothing", "Data Smoothing", "Smooth the instrument data (on/off)", True)
	cat1.AddList("SmoothingFactor", "Data Smoothing", "Select the data smoothing factor.", _
	"75%", Array As String("No Smoothing", "25%", "50%", "75%", "Max Smoothing"))

	cat2.Initialize("Other")
	cat2.AddEditText("bleName", "Anemometer Name", "You can rename your Anemometer and save it for furture connections.", Starter.defaultSensorName)
	cat2.AddCheckBox("ErrReporting", "Error Reporting", "Allow App to send Crash reports (on/off).", False)
		
	'add the categories to the main prefScreen
	Starter.prefScreen.AddPreferenceCategory(cat1)
	Starter.prefScreen.AddPreferenceCategory(cat2)
	
	' initialize the preference values with the Defaults if they have not been initialized
	If Starter.prefManager.GetAll.Size = 0 Then Set_Default_User_Preferences
End Sub


Sub Set_Default_User_Preferences
	'defaults are only set on the first run.
	Starter.prefManager.SetString("SampleRate", "4 samples/second")
	Starter.prefManager.SetString("SmoothingFactor", "75%")
	Starter.prefManager.SetBoolean("ErrReporting", False)
	Starter.prefManager.SetBoolean("TrueNorth", True)
	Starter.prefManager.SetString("bleName", Starter.defaultSensorName)
	Starter.prefManager.SetString("bleMAC", Starter.invalidMAC)
End Sub


Sub Get_Current_Preference_Settings(verbose As Boolean)
	Select Starter.prefManager.GetString("SampleRate")
		Case "1 sample/second"
			Starter.prefSampleRate = 1
		Case "4 samples/second"
			Starter.prefSampleRate = 4
		Case "8 samples/second"
			Starter.prefSampleRate = 8
		Case Else
			Starter.prefSampleRate = 4
	End Select
	Select Starter.prefManager.GetString("SmoothingFactor")
		Case "No Smoothing"
			Starter.prefSmoothing = 1.0
		Case "25%"
			Starter.prefSmoothing = 0.45
		Case "50%"
			Starter.prefSmoothing = 0.35
		Case "75%"
			Starter.prefSmoothing = 0.25
		Case "Max Smoothing"
			Starter.prefSmoothing = 0.15
		Case Else
			Starter.prefSmoothing = 0.25
	End Select
	Starter.prefErrReporting = Starter.prefManager.GetBoolean("ErrReporting")
	Starter.prefTrueNorth = Starter.prefManager.GetBoolean("TrueNorth")
	
	If (Starter.prefManager.GetString("bleMAC") = Starter.invalidMAC) Then
		Starter.prefBluetoothName = Starter.defaultSensorName
		Starter.prefBluetoothMAC = Starter.invalidMAC
	Else
		Starter.prefBluetoothName = Starter.prefManager.GetString("bleName")
		Starter.prefBluetoothMAC = Starter.prefManager.GetString("bleMAC").Replace("-", "")
	End If
	
	Starter.calcTools.Initialize
	
	If verbose Then
		Log("Main->Get_Current_Preference_Settings(): Sample Rate: "&Starter.prefSampleRate)
		Log("Main->Get_Current_Preference_Settings(): Smoothing F: "&Starter.prefSmoothing)
		Log("Main->Get_Current_Preference_Settings(): Err Report : "&Starter.prefErrReporting)
		Log("Main->Get_Current_Preference_Settings(): True North:  "&Starter.prefTrueNorth)
		Log("Main->Get_Current_Preference_Settings(): Mac Name:    "&Starter.prefBluetoothName)
		Log("Main->Get_Current_Preference_Settings(): Mac Add:     "&Starter.prefBluetoothMAC)
	End If
End Sub


'=================================START OF JAVA CODE================================
#If JAVA
public boolean setBluetooth() {
    android.bluetooth.BluetoothAdapter bluetoothAdapter = android.bluetooth.BluetoothAdapter.getDefaultAdapter();
    boolean isEnabled = bluetoothAdapter.isEnabled();

    if (!isEnabled) {
        return bluetoothAdapter.enable();
    }
    // No need To change bluetooth state
    return true;
}

// confirmation sprint i1437327141

public float getDeclinationJava(double Lat, double Lon, double Altitude, long Millis) {
	import android.hardware.GeomagneticField;
	GeomagneticField mGeoField = new GeomagneticField((float)Lat, (float)Lon, (float)Altitude, Millis);
    return mGeoField.getDeclination();	
}
#End If
'=================================END OF JAVA CODE==================================
